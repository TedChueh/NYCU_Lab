	ORG 00H
	AJMP INIT
	ORG 50H

SEG_DATA:
	DB 03H
	DB 09FH
	DB 025H
	DB 0DH
	DB 099H
	DB 049H
	DB 041H
	DB 01FH
	DB 01H
	DB 09H
	DB 011H 
	DB 0C1H 
	DB 063H
	DB 085H 
	DB 061H
	DB 071H
	DB 0FFH

KEYPAD:
	DB 00H
	DB 04H  
	DB 08H 
	DB 0CH  

	DB 01H  
	DB 05H  
	DB 09H   
	DB 0DH

	DB 02H  
	DB 06H  
	DB 0AH
	DB 0EH

	DB 03H   
	DB 07H  
	DB 0BH  
	DB 0FH

	DB 010H ; No key pressed

INIT:
    MOV TMOD, #01H 
    MOV R0, #010H ;Col Detection
	MOV R1, #00H  ;Row Detection
	MOV R2, #00H  ;Temp For Row Detection
	MOV R3, #00H  ;Display Shift Control

	MOV R4, #00H  ;FSM Status
	MOV R5, #010H ;Display0 value
	MOV R6, #010H ;Display1 value
	MOV R7, #010H ;Last FSM0 value

    MOV P0, #0FFH ;Keypad Input
	MOV P1, #0FFH ;Segment Output
	MOV P2, #11111110B ;Display IO Control

LOOP:
    ACALL DETECT
	ACALL DISPLAYFSM
	ACALL DISPLAY
	JMP LOOP

DETECT:
	MOV R0, #010H
	MOV R1, #00H

DETECTCOL0:
    MOV P0, #07FH
	ACALL DELAY
	MOV A, P0
    ANL A, #0FH
	MOV R2, A
	CLR C
	SUBB A, #0FH
    JZ DETECTCOL1
	MOV R0, #00H
	ACALL DETECTROW
	RET

DETECTCOL1:
	MOV P0, #0BFH
	ACALL DELAY
	MOV A, P0
	ANL A, #0FH
	MOV R2, A
	CLR C
	SUBB A, #0FH
	JZ DETECTCOL2
	MOV R0, #04H
	ACALL DETECTROW
	RET

DETECTCOL2:
	MOV P0, #0DFH
	ACALL DELAY
	MOV A, P0
	ANL A, #0FH
	MOV R2, A
	CLR C
	SUBB A, #0FH
	JZ DETECTCOL3	
	MOV R0, #08H
	ACALL DETECTROW
	RET

DETECTCOL3:
	MOV P0, #0EFH
	ACALL DELAY
	MOV A, P0
	ANL A, #0FH
	MOV R2, A
	CLR C
	SUBB A, #0FH
	JZ DEFAULT_NOPRESSED
	MOV R0, #0CH
	ACALL DETECTROW
	RET
;------------------------------------------------------------------

DEFAULT_NOPRESSED:
	RET

DETECTROW:
	CJNE R2, #0EH,  NOTPRESSEDROW0
	MOV R1, #00H
	RET

NOTPRESSEDROW0:
	CJNE R2, #0DH,  NOTPRESSEDROW1
	MOV R1, #01H
	RET

NOTPRESSEDROW1:
	CJNE R2, #0BH,  NOTPRESSEDROW2
	MOV R1, #02H
	RET

NOTPRESSEDROW2:
	CJNE R2, #07H,  DEFAULT
	MOV R1, #03H
	RET

DEFAULT:
	RET
;------------------------------------------------------------------

DISPLAYFSM:

DISPLAYFSM0:
	CJNE R4, #00H, DISPLAYFSM1
	MOV DPTR, #KEYPAD
	MOV A, R0
	ADD A, R1
	MOVC A, @A+DPTR
	CLR C
	SUBB A, #0AH
	JNC FSMDEFAULT

	MOV DPTR, #KEYPAD
	MOV A, R0
	ADD A, R1
	MOVC A, @A+DPTR
	MOV R5, A
	MOV R7, A
	MOV R4, #01H
	RET

DISPLAYFSM1:
	CJNE R4, #01H, DISPLAYFSM2
	MOV R6, #010H
	MOV DPTR, #KEYPAD
	MOV A, R0
	ADD A, R1
	MOVC A, @A+DPTR
	CLR C
	SUBB A, #0AH
	JNC FSMDEFAULT

	MOV DPTR, #KEYPAD
	MOV A, R0
	ADD A, R1
	MOVC A, @A+DPTR
	CLR C
	SUBB A, R7
	JZ FSMDEFAULT

	MOV DPTR, #KEYPAD
	MOV A, R0
	ADD A, R1
	MOVC A, @A+DPTR
	MOV P3, R5
	MOV R5, A
	MOV R4, #02H
	RET

DISPLAYFSM2:
	CJNE R4, #02H, FSMDEFAULT
	MOV DPTR, #KEYPAD
	MOV A, R0
	ADD A, R1
	MOVC A, @A+DPTR
	CJNE A, #0AH, FSMDEFAULT
	MOV R6, P3
	MOV R4, #00H
	RET

FSMDEFAULT:
	RET

;------------------------------------------------------------------
DISPLAY:
	MOV DPTR, #SEG_DATA

DISPLAY0:
	CJNE R3, #00H, DISPLAY1
	MOV A, R5
	MOVC A, @A+DPTR
	MOV P1, A
	MOV P2, #11111110B 
	MOV R3, #01H
	RET

DISPLAY1:
	CJNE R3, #01H, DISPLAYDEFAULT
	MOV A, R6
	MOVC A, @A+DPTR
	MOV P1, A
	MOV P2, #11111101B 
	MOV R3, #00H
	RET

DISPLAYDEFAULT:
	MOV R3, #00H
	RET
;------------------------------------------------------------------

DELAY:
	MOV TH0, #0FBH
	MOV TL0, #0CEH
	SETB TR0	
	JNB TF0, $
	CLR TR0
	CLR TF0
	RET
	END